buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.5.0'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.7.0'
        classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.3.0'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
        classpath 'com.toomuchcoding:uptodate-gradle-plugin:1.0.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'ru.vyarus.animalsniffer'
apply plugin: 'com.toomuchcoding.uptodate'

scmVersion {
    tag {
        prefix = 'release'
        versionSeparator = '/'
    }
    versionIncrementer 'incrementMinor'
    releaseCommitMessage { version, position -> "Release version: ${version}\n\n[ci skip]" }
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /"io\.codearte\.gradle\.nexus:gradle-nexus-staging-plugin:.*"/},
                           replacement: {v, p -> "\"io.codearte.gradle.nexus:gradle-nexus-staging-plugin:$v\""}]
        pre 'commit', { version, position -> "Release version: ${version}\n\n[ci skip]" }
    }
}

project.version = scmVersion.version

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {   //TODO: Spock 1.1 breaks nebula-test - https://github.com/spockframework/spock/issues/714
        //groovy 2.3.x or 2.4.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'info.solidsoft.spock:spock-global-unroll:0.5.1'
    testCompile 'cglib:cglib-nodep:3.2.5'   //for Spying in Spock
    testCompile 'junit:junit:4.12'

    /*funcT*/testCompile('com.netflix.nebula:nebula-test:5.1.3') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    /*funcT*/testCompile 'com.github.tomakehurst:wiremock:1.50' //1.54+ makes GroovyBuilder to fail with parsing empty response on POST
    signature 'org.codehaus.mojo.signature:java17:1.0@signature'
}

tasks.withType(ru.vyarus.gradle.plugin.animalsniffer.AnimalSniffer) {
    onlyIf { project.hasProperty('compatibility') }
}

nexusStaging {
    packageGroup = "io.codearte"
    stagingProfileId = "93c08fdebde1ff"
}


modifyPom {
    project {
        name 'Gradle Nexus staging plugin'
        description 'Gradle Nexus staging plugin'
        url 'https://github.com/Codearte/gradle-nexus-staging-plugin'
        inceptionYear '2015'

        scm {
            url 'https://github.com/Codearte/gradle-nexus-staging-plugin'
            connection 'scm:https://github.com/Codearte/gradle-nexus-staging-plugin.git'
            developerConnection 'scm:git://github.com/Codearte/gradle-nexus-staging-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'developer'
                    role 'despot'
                }
            }
        }
    }
}

wrapper {
    gradleVersion "2.4"
}

if (hasProperty('pluginPortal')) {

    apply plugin: 'com.gradle.plugin-publish'

    pluginBundle {
        website = 'https://github.com/Codearte/gradle-nexus-staging-plugin/'
        vcsUrl = 'https://github.com/Codearte/gradle-nexus-staging-plugin/'

        plugins {
            plugin {
                id = 'io.codearte.nexus-staging'
                displayName = 'gradle-nexus-staging-plugin'
                description = 'Releasing to Maven Central from Gradle without dealing with Nexus UI'
                tags = ['maven-central', 'nexus', 'release', 'binary', 'repository', 'staging', 'codearte']
            }
        }

        mavenCoordinates {
            groupId = project.group
            artifactId = project.name
        }
    }
}
