buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.2.3'
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3'
    }
}

apply plugin: 'groovy'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

scmVersion {
    tag {
        prefix = 'release'
        versionSeparator = '/'
    }
    createReleaseCommit = true
    releaseCommitMessage { version, position -> "Release version: ${version}\n\n[ci skip]" }
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /"io\.codearte\.gradle\.nexus:gradle-nexus-staging-plugin:.*"/},
                           replacement: {v, p -> "\"io.codearte.gradle.nexus:gradle-nexus-staging-plugin:$v\""}]
    }
}

project.version = scmVersion.version

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile("org.codehaus.groovy.modules.http-builder:http-builder:0.7.1") {
        exclude group: 'xerces', module: 'xercesImpl'   //conflicts with xercesImpl from Gradle
    }

    testCompile('org.spockframework:spock-core:1.0-groovy-2.3') {
        //groovy 2.3.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'info.solidsoft.spock:spock-global-unroll:0.5.0'
    testCompile 'cglib:cglib-nodep:2.2.2'   //for Spying in Spock
    testCompile 'junit:junit:4.11'

    /*funcT*/testCompile('com.netflix.nebula:nebula-test:2.2.1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    /*funcT*/testCompile('com.github.tomakehurst:wiremock:1.50') {  //1.51+ requires httpclient 4.3+ which conflicts with version distributed with Gradle
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

nexusStaging {
    packageGroup = "io.codearte"
    stagingProfileId = "93c08fdebde1ff"
}


modifyPom {
    project {
        name 'Gradle Nexus staging plugin'
        description 'Gradle Nexus staging plugin'
        url 'https://github.com/Codearte/gradle-nexus-staging-plugin'
        inceptionYear '2015'

        scm {
            url 'https://github.com/Codearte/gradle-nexus-staging-plugin'
            connection 'scm:https://github.com/Codearte/gradle-nexus-staging-plugin.git'
            developerConnection 'scm:git://github.com/Codearte/gradle-nexus-staging-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'developer'
                    role 'despot'
                }
            }
        }
    }
}

wrapper {
    gradleVersion "2.4"
}
