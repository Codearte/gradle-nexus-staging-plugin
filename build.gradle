buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.2.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.bmuschko.nexus'

scmVersion {
    tag { prefix = '' }
    createReleaseCommit = true
    releaseCommitMessage { version, position -> "Release version: ${version}\n\n[ci skip]" }
}

project.version = scmVersion.version

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile("org.codehaus.groovy.modules.http-builder:http-builder:0.7.1") {
        exclude group: 'xerces', module: 'xercesImpl'   //conflicts with xercesImpl from Gradle
    }

    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        //groovy 2.3.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.2'   //for Spying in Spock
    testCompile 'junit:junit:4.11'

    /*funcT*/testCompile('com.netflix.nebula:nebula-test:2.0.5') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

modifyPom {
    project {
        name 'Gradle Nexus staging plugin'
        description 'Gradle Nexus staging plugin'
        url 'https://github.com/Codearte/gradle-nexus-staging-plugin'
        inceptionYear '2015'

        scm {
            url 'https://github.com/Codearte/gradle-nexus-staging-plugin'
            connection 'scm:https://github.com/Codearte/gradle-nexus-staging-plugin.git'
            developerConnection 'scm:git://github.com/Codearte/gradle-nexus-staging-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
            }
        }
    }
}

wrapper {
    gradleVersion "2.3"
}
